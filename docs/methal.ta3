// el ta3lee2at beyhmelha el compiler
/* ta3lee2 
    fe
    akter
    men 
    satr
*/

// el anwa3 al assasiah: 
// sa7e7: i.e. -50, -10, 0, 20, 1000
// 7a2i2i: i.e. -50.9, -10.0, 0.0, 20.1, 1000.9999

// momkn te3arraf mota8ier/thabet 3alamy
sa7e7 s = 5
thabet 7a2i2i M = 5.0

// el dawal lazem tokon met3arrafa 2abl ma tenadihom
sa7e7 deef2(sa7e7 i) {
    basy i + 2
}

// enum = tarqeem
// kol elle feeh unique lel tarqeem bas
// ya3ne low 3arraft X marrah gowa Tarqeem1 we Tarqeem2, keda 3andak Tarqeem1::X,Tarqeem2::X
tarqeem IPVersion {
    V4, //=0
    V6  //=1
}

// `bedayh` heya awl dallah betednedeh fel barnameg
// el code elly bytbasah menha howa el exit code
sa7e7 bedayah() {
    s = s + 1

    lw s == 6 {
        // zy ma ana tawqa3t
    } 8ero lw s >= 4 {
        // sa7 bardo
    } 8ero {
        // yasater ya rab!
        basy 1 // wa2af kol 7aga, we basy err
    }

    // mesh lazem semicolons, bene3ref en el expression 5eles men nehayat el satr
    // ella low fe aktar men statement fee nafs el satr
    // aw } fe nafs el satr
    { sa7e7 x = 5; x = x + 5; }

    lw s > 5 aw s < 5 {
        // kol 7agah ella 5
    }

    lw msh s == 6 {
        // yasater ya rab!
        basy 1
    }

    lw s == 6 we s == 7 {
        // ezay mesh fahem :O
        basy 2
    }

    sa7e7 g = 6
    g fe 7alet 5 {
        // e3mel 7agah
    } 7alet 6 {
        // e3mel 7agah
        // howa da elly ha yetnada
    } 8ero {
        // e3mel 7agah low el ba2y matnafezzosh
    }

    // 0 le 10
    lef sa7e7 i = 0; i <= 9; i = i + 1 {
        // e3mel 7agah
    }

    // 9 le 0
    sa7e7 h = 0
    lef h = 9; h >= 0; h = h - 1 {
        // e3mel 7agah
    }

    // 10 le 1
    sa7e7 i = 10
    talma i != 0 {
        // e3mel 7agah
        i = i - 1
    }

    // 5 le 16
    sa7e7 j = 5
    karrar {
        // do some thing
        j = j + 1
    } l7d j == 16

    // ta7weel
    s = sa7e7(M)
    7a2i2i myfloat = 7a2i2i(s % 10)

    // 3amalyat riadiah
    sa7e7 x = 0
    // x + 1 // 1
    // (x + 5) / 2 // 2
    // (x + 5) / 2.0 // 2.5
    // 5 ** 2 // 25
    // (((((5+1))))) // 6
    x = x + 5
    // (x % 4) // 1
    // (x * 10) // 50
    // (x - 6) // -1

    // dawal
    sa7e7 x2 = deef2(5) // x2=7

    sa7e7 y = 5
    // y = 5
    {
        7a2i2i y = -99.0
        // y = -99.0

        {
            sa7e7 mm = 10
            // mm = 10
        }
        // mm barrah el ta8tyah
    }
    // y + 2 = 7
    // mm barrah el ta8tyah

    IPVersion v = IPVersion::V6

    // keda nege7na
    basy 0
}
